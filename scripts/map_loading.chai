class MapLoading {
  attr thiss
  attr animation_map
  attr scene_generator
  attr sprite_movement
  attr map_name

  def MapLoading() {
    if(this.map_name.is_var_undef()) {
      this.map_name = ""
    }
  }

  def setScriptObjectHandle(handle) {
    this.thiss := handle
    set_serializable(this.thiss, "map_name")
  }

  def onStart() {
    this.animation_map = Map(config.getString("animation_database"))
    this.scene_generator = SceneGenerator(this.animation_map, texture_manager, shader_manager)
    var map_names = config.getStringVector("maps")

    for(var i = 0; i < map_names.size(); ++i) {
      var map_path = map_names[i]
      var map = Map(map_path)
      var scene = this.scene_generator.createSceneFromMap(config.getInt("patch_width"), config.getInt("patch_height"), map)
      var name = this.scene_generator.getStrippedMapName(map_path)
      scene.setName(name)
      engine.addScene(scene)
    }

    var sprite = this.scene_generator.getDynamicEntityByName("Aidan")

    this.sprite_movement = SpriteMovement()
    this.sprite_movement.setAnimationStringState(SpriteState_MOVE_UP, "Up_Movement")
    this.sprite_movement.setAnimationStringState(SpriteState_MOVE_DOWN, "Down_Movement")
    this.sprite_movement.setAnimationStringState(SpriteState_MOVE_LEFT, "Left_Movement")
    this.sprite_movement.setAnimationStringState(SpriteState_MOVE_RIGHT, "Right_Movement")
    this.sprite_movement.setAnimationStringState(SpriteState_FACE_UP, "Up_Still")
    this.sprite_movement.setAnimationStringState(SpriteState_FACE_DOWN, "Down_Still")
    this.sprite_movement.setAnimationStringState(SpriteState_FACE_LEFT, "Left_Still")
    this.sprite_movement.setAnimationStringState(SpriteState_FACE_RIGHT, "Right_Still")
    this.sprite_movement.addObserver(camera)
    this.sprite_movement.setMovingSpeed(2.0)

    sprite.addComponent(this.sprite_movement)
    input_system.addObserver(this.sprite_movement)
    camera.getTransform().translate(sprite.getTransform().getAbsoluteTranslation())
    
    sprite.setActive(true)

    component_manager.addComponents(sprite.getComponents())
    if(this.map_name == "") {
      print("loading map from config")
      print("that map is: " + config.getString("active_map"))
      this.map_name = config.getString("active_map")
    }
    else {
      print("loading map from save")
    }

    var active_scene = engine.findSceneByName(this.map_name)
    this.sprite_movement.addCollisionData(active_scene.getCollisionData())
    
    this.thiss.notify(LoadMapEvent_create(this.map_name))
  }

  def onUpdate(delta) {
  }

  def onDestroy() {

  }

  def handleEvent(event) {
    switch(event.getEventType()) {
      case(EventType_LOAD_MAP) {
        if(this.map_name != "") {
          engine.deactivateScene(this.map_name)
        }

        this.map_name = event.getName()

        engine.activateScene(this.map_name)
       
        break;
      }
      default {

      }
    }
  }
}